plugins {
    id 'java'
    id 'application'
}

mainClassName = 'oop.project.MapApp'
version = '0.0.1'

sourceSets {
  main {
    resources {
        srcDirs = ["src/main/java"]
        includes = ["**/*.fxml", "**/*.css", "**/*.csv"]
    }
    output.resourcesDir = sourceSets.main.output.classesDir
  }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.transit:gtfs-realtime-bindings:0.0.4'
    compile 'com.twilio.sdk:twilio-java-sdk:4.0.2'
    compile 'com.lynden:GMapsFX:1.1.1'

    compile files('lib/cmu_time_awb.jar')
    compile files('lib/cmu_us_kal.jar')
    compile files('lib/cmudict04.jar')
    compile files('lib/cmulex.jar')
    compile files('lib/cmutimelex.jar')
    compile files('lib/en_us.jar')
    compile files('lib/freetts-jsapi10.jar')
    compile files('lib/freetts.jar')
    compile files('lib/mbrola.jar')
}

//custom settings and tasks
task fatJar(type: Jar) {
    description "Creates a single jar that includes all dependencies"
    mustRunAfter = ['jar']
    baseName = project.name + '-fat'
    manifest {
        attributes 'Implementation-Title': 'OOP-Project',
                   'Implementation-Version': version,
                   'Main-Class': 'oop.project.MapApp'
    }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

task localize(type: Sync) {
    description "Syncs project dependencies into './bin'"
    from configurations.runtime
    into './bin'
}

//ensure fatJar is run after jar
jar.finalizedBy fatJar

//show detailed compilation warnings
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}
